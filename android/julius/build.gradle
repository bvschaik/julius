apply plugin: 'com.android.application'

buildDir = file("../distribution/${rootProject.name}/${project.name}")

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    defaultConfig {
        applicationId "bvschaik.julius"
        minSdkVersion 21
        targetSdkVersion 28

        def longVersionName = "git describe --tags --dirty".execute().text.trim().replace('v', '')
        def (fullVersionTag) = longVersionName.tokenize('-')
        def (versionMajor, versionMinor, versionPatch) = fullVersionTag.tokenize('.')
        versionName longVersionName
        versionCode versionMajor.toInteger() * 10000 + versionMinor.toInteger() * 100 + versionPatch.toInteger()

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                arguments "-DANDROID_BUILD=ON"
            }
        }
        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
        }
        sourceSets {
            main {
                java {
                    srcDir 'src/main/java'
                    srcDir '../../ext/SDL2'
                    exclude '**/android-project-ant/**'
                }
                res {
                    srcDir '../../res/android'
                }
            }
        }
    }
    buildTypes {
        debug {
            ext.alwaysUpdateBuildId = false
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "../../CMakeLists.txt"
        }
    }
}

repositories {
    google()
    jcenter()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation project(':SDL2')
    testImplementation 'junit:junit:4.12'
}
